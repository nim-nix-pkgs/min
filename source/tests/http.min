'test require :test
;;;

"http" *test/describe
  
  "postman-echo.com"        :host
  "https://$1" (host) => %   :url

  ("$1/get" (url) => % get-content from-json /headers /user-agent  "min http-module/$1" (version) => % ==) *test/assert

  ("$1/get?test=Hello!" (url) => % "tests/test1.json" :file file download file fread from-json /args /test "Hello!" ==) *test/assert
  "tests/test1.json" rm

  (
    {} (
      ("$1/get" (url) => % %url) 
      ("GET" %method)
      ({"it-it" :Accept-Language} %headers)
      (request)
    ) tap /body from-json /headers /accept-language "it-it" ==
  ) *test/assert

  (
    {} (
      ("$1/put" (url) => % %url) 
      ("PUT" %method)
      ({} to-json %body)
      (request)
    ) tap /body from-json /data {} ==
  ) *test/assert

  (
    {} (
      ("$1/post" (url) => % %url) 
      ("POST" %method)
      ({"post" :test} to-json %body)
      (request)
    ) tap /headers /content-type "^application/json" match?
  ) *test/assert

  (
    {} (
      ("$1/patch" (url) => % %url) 
      ("PATCH" %method)
      ({} to-json %body)
      (request)
    ) tap /body from-json /data {} ==
  ) *test/assert

  (
    {} (
      ("$1/delete" (url) => % %url) 
      ("DELETE" %method)
      (request)
    ) tap /body from-json /url "https://$1/delete" (host) => % ==
  ) *test/assert

  *test/report
  clear-stack